WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

/// LITERALS
int   = @{ "-"? ~ ("0" | '1'..'9' ~ '0'..'9'*) }
float = @{
    "-"? ~ ("0" ~ "." ~ '0'..'9'+ | '1'..'9' ~ '0'..'9'* ~ "." ~ '0'..'9'+)
}

double_quoted_string     = @{ "\"" ~ (!("\"") ~ ANY)* ~ "\"" }
single_quoted_string     = @{ "\'" ~ (!("\'") ~ ANY)* ~ "\'" }
backquoted_quoted_string = @{ "`" ~ (!("`") ~ ANY)* ~ "`" }

string = @{
    double_quoted_string
  | single_quoted_string
  | backquoted_quoted_string
}

boolean = { "true" | "false" }

literal = { boolean | string | float | int | array }
array   = { "[" ~ (literal ~ ",")* ~ literal? ~ "]" }

/// Idents

all_chars = _{ 'a'..'z' | 'A'..'Z' | "_" | '0'..'9' }
ident     = @{
    ('a'..'z' | 'A'..'Z' | "_") ~ all_chars*
}

kwarg  =  { ident ~ "=" ~ literal }
kwargs = _{ kwarg ~ ("," ~ kwarg)* }

inline_element_tags =  { "content" | "template" | "component" }
inline_element      = !{ "<" ~ inline_element_tags ~ kwargs? ~ "/>" }

block_element_tags =  { "template" | "foreach" | "if" }
block_element      = !{
    "<template" ~ ident ~ kwargs? ~ ">"
  | "<foreach" ~ ident ~ "in" ~ ident ~ ">"
  | "</" ~ block_element_tags ~ ">"
}

var_element = !{ "{{" ~ ident ~ "}}" }

element = !{
    block_element
  | inline_element
  | var_element
}

text = ${ (!(element) ~ ANY)+ }

content = _{
    element
  | text
}

root = ${ SOI ~ content* ~ EOI }
